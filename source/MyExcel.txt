' 対象リクエスト対象定義
Type TargetRequestInfo
    SheetNames() As String
End Type


' 取得リクエスト対象定義
Type JsonRequestTargetInfo
    Key As String               ' Json出力時のキー（object用）
    Target As String            ' 対象（rows例:"A"/cols例:"1"）
End Type

' 取得リクエスト定義
Type JsonRequestInfo
    RangeType As String                  ' 取得方法（cell:対象セルのみ/rows:対象セル以降の行/cols:対象セル以降の列/sheet:シート名）
    Key As String                        ' Json出力時のキー
    Cell As String                       ' 対象セル
    Type As String                       ' 取得対象の型（rows/cols用。object:連想配列/それ以外:配列）
    Targets() As JsonRequestTargetInfo   ' 取得対象
    Count As Integer                     ' 取得数（rows/cols用。空もしくは0以下設定時、Cell（対象セル）から空文字かNullが出現するまで取得）
End Type

' 出力ファイルリクエスト定義
Type ExportFileRequestInfo
    Type As String                  ' 取得方法（cell:対象セルのみ/sheet:シート名/custom:カスタム）
    Cell As String                  ' 対象セル
    FileName As String              ' ファイル名（custom例:"sampleFile"。未指定時、シート名。拡張子不要）
    FileType As String              ' ファイル形式（json/xml。未指定時、json）
End Type

' セル範囲定義
Type CellRangeInfo
    StartCell As String
    StartCol As String
    StartColIndex As Integer
    StartRow As Integer
    EndCell As String
    EndCol As String
    EndColIndex As Integer
    EndRow As Integer
    ColCount As Integer
    RowCount As Integer
End Type

' コンフィグ
Type Config
    TargetRequest As TargetRequestInfo
    JsonRequests() As JsonRequestInfo
    ExportFileRequest As ExportFileRequestInfo
End Type

' 出力実行
Sub ExecuteExportFile(strWsPath As String, strConfigPath As String)
    Dim wb As Workbook: Set wb = Workbooks.Open(strWsPath)
    
    'コンフィグ読込
    Dim typConfig As Config: typConfig = LoadConfig(wb, strConfigPath)
    
    ' ファイル出力
    Call ExportFile(wb, typConfig)
    
    wb.Close
End Sub

' コンフィグ読込
Function LoadConfig(wb As Workbook, strConfigPath As String) As Config
    Dim strFileText As String: strFileText = MyCommon.ReadTextFileToString(strConfigPath)
    
    ' JSON変換
    Dim dicConfig As Dictionary: Set dicConfig = JsonConverter.ParseJson(strFileText)
    
    ' コンフィグ読込
    Dim typConfig As Config
    
    ' コンフィグ読込（対象リクエスト）
    Call LoadConfigTargetRequest(wb, dicConfig, typConfig)
    
    ' コンフィグ読込（取得リクエスト）
    Call LoadConfigJsonRequest(wb, dicConfig, typConfig)
    
    ' コンフィグ読込（出力ファイルリクエスト）
    Call LoadConfigExportFileRequest(wb, dicConfig, typConfig)
    
    LoadConfig = typConfig
End Function

' コンフィグ読込（対象リクエスト）
Sub LoadConfigTargetRequest(wb As Workbook, dicConfig As Dictionary, typConfig As Config)
    Dim lstSheetNames() As String
    Dim indexSheetName As Integer: indexSheetName = 0
    Dim dicConfigTargetRequest As Dictionary: Set dicConfigTargetRequest = dicConfig("TargetRequest")
    
    ' シート名格納
    If dicConfigTargetRequest.Exists("SheetNames") And dicConfigTargetRequest("SheetNames").Count > 0 Then
        'シート名退避
        Dim TargetRequestSheetName As Object
        For Each TargetRequestSheetName In dicConfigTargetRequest("SheetNames")
            ReDim Preserve lstSheetNames(indexSheetName)
            lstSheetNames(indexSheetName) = TargetRequestSheetName
            indexSheetName = indexSheetName + 1
        Next TargetRequestSheetName
    Else
        'シート名が空の場合、すべてのシートを対象にする
        For Each ws In wb.Worksheets
            ReDim Preserve lstSheetNames(indexSheetName)
            lstSheetNames(indexSheetName) = ws.Name
            indexSheetName = indexSheetName + 1
        Next ws
    End If

    ' 開始/終了シート名判定
    Dim blnIsSheetNameContinue As Boolean
    If Not MyCommon.IsNullOrEmpty(dicConfigTargetRequest("FirstSheetName")) Then
        blnIsSheetNameContinue = True
    End If
    Dim SheetName As String
    Dim i As Integer
    ' 開始〜終了シートを格納
    indexSheetName = 0
    For i = LBound(lstSheetNames) To UBound(lstSheetNames)
        SheetName = lstSheetNames(i)
        If Not MyCommon.IsNullOrEmpty(dicConfigTargetRequest("FirstSheetName")) And dicConfigTargetRequest("FirstSheetName") = SheetName Then
            blnIsSheetNameContinue = False
        End If
        If blnIsSheetNameContinue Then
            GoTo SheetNameContinueLoop
        End If
        ReDim Preserve typConfig.TargetRequest.SheetNames(indexSheetName)
        typConfig.TargetRequest.SheetNames(indexSheetName) = SheetName
        indexSheetName = indexSheetName + 1
        If Not MyCommon.IsNullOrEmpty(dicConfigTargetRequest("LastSheetName")) And dicConfigTargetRequest("LastSheetName") = SheetName Then
            Exit For
        End If
SheetNameContinueLoop:
    Next i
    
    ' 例外判定
    If indexSheetName = 0 Then
        Err.Raise 9999, "CustomError", "Config:ReuqestTargetから対象シートを特定できませんでした。"
    End If
End Sub

' コンフィグ読込（取得リクエスト）
Sub LoadConfigJsonRequest(wb As Workbook, dicConfig As Dictionary, typConfig As Config)
    ' 例外判定
    If IsEmpty(dicConfig("JsonRequests")) Then
        Err.Raise 9999, "CustomError", "Config:JsonRequestsは必須です"
    End If
    
    Dim indexJsonRequest As Integer: indexJsonRequest = 0
    For Each JsonRequest In dicConfig("JsonRequests")
        Dim typJsonRequest As JsonRequestInfo
        typJsonRequest.RangeType = JsonRequest("RangeType")
        If MyCommon.IsNullOrEmpty(typJsonRequest.RangeType) Then
            typJsonRequest.RangeType = "cell"
        End If
        typJsonRequest.Key = JsonRequest("Key")
        typJsonRequest.Cell = JsonRequest("Cell")
        If MyCommon.IsNullOrEmpty(typJsonRequest.Cell) Then
            If typJsonRequest.RangeType <> "sheet" Then
                Err.Raise 9999, "CustomError", "Config:Cellは必須です"
            End If
        End If
        typJsonRequest.Type = JsonRequest("Type")
        If typJsonRequest.RangeType = "row" Or typJsonRequest.RangeType = "col" Then
            typJsonRequest.Type = "object"
        End If
        
        If Not IsEmpty(JsonRequest("Targets")) Then
            typJsonRequest.Type = JsonRequest("Type")
            Dim indexJsonRequestTarget As Integer: indexJsonRequestTarget = 0
            For Each JsonRequestTarget In JsonRequest("Targets")
                Dim typJsonRequestTarget As JsonRequestTargetInfo
                typJsonRequestTarget.Key = JsonRequestTarget("Key")
                typJsonRequestTarget.Target = JsonRequestTarget("Target")
                ReDim Preserve typJsonRequest.Targets(indexJsonRequestTarget)
                typJsonRequest.Targets(indexJsonRequestTarget) = typJsonRequestTarget
                indexJsonRequestTarget = indexJsonRequestTarget + 1
            Next JsonRequestTarget
        Else
            ' 例外判定
            If typJsonRequest.Type = "object" Then
                Err.Raise 9999, "CustomError", "Config:Targetsは必須です"
            End If
        End If
        typJsonRequest.Count = JsonRequest("Count")
        
        ReDim Preserve typConfig.JsonRequests(indexJsonRequest)
        typConfig.JsonRequests(indexJsonRequest) = typJsonRequest
        indexJsonRequest = indexJsonRequest + 1
    Next
End Sub

' コンフィグ読込（出力ファイルリクエスト）
Sub LoadConfigExportFileRequest(wb As Workbook, dicConfig As Dictionary, typConfig As Config)
    Dim typExportFileRequest As ExportFileRequestInfo
    If Not IsEmpty(dicConfig("ExportFileRequest")) Then
        typExportFileRequest.Type = dicConfig("ExportFileRequest")("Type")
        typExportFileRequest.Cell = dicConfig("ExportFileRequest")("Cell")
        typExportFileRequest.FileName = dicConfig("ExportFileRequest")("FileName")
        typExportFileRequest.FileType = dicConfig("ExportFileRequest")("FileType")
    Else
        typExportFileRequest.Type = "sheet"
        typExportFileRequest.FileType = "json"
    End If
    typConfig.ExportFileRequest = typExportFileRequest
End Sub

' JSONファイル出力
Sub ExportFile(wb As Workbook, typConfig As Config)
    For Each SheetName In typConfig.TargetRequest.SheetNames
        ' デバッグ用
        Debug.Print SheetName
        
        ' 対象シート取得
        Dim wsTargetSheet As Worksheet: Set wsTargetSheet = wb.Sheets(SheetName)
        
        ' Jsonファイル出力
        Call ExportSheetToFile(wsTargetSheet, typConfig)
    Next
End Sub

' JSONファイル出力（シート）
Sub ExportSheetToFile(wsTargetSheet As Worksheet, typConfig As Config)
    ' Jsonデータ作成
    Dim dicJson As Dictionary: Set dicJson = CreateJson(wsTargetSheet, typConfig)
    
    ' ファイル出力
    Dim strFileName As String
    If typConfig.ExportFileRequest.FileType = "xml" Then
        strFileName = MyExcel.GetExportFileName(wsTargetSheet, typConfig, "xml")
        Call MyCommon.ExportToXML(dicJson, strFileName)
    Else
        strFileName = MyExcel.GetExportFileName(wsTargetSheet, typConfig, "json")
        Call MyCommon.ExportToJson(dicJson, strFileName)
    End If
    
End Sub

' JSONデータ作成
Function CreateJson(wsTargetSheet As Worksheet, typConfig As Config) As Dictionary
    Dim dicJson As Dictionary: Set dicJson = CreateObject("Scripting.Dictionary")
    
    Dim indexJsonRequest As Integer: indexJsonRequest = 0
    For indexJsonRequest = 0 To UBound(typConfig.JsonRequests)
        Dim JsonRequest As JsonRequestInfo: JsonRequest = typConfig.JsonRequests(indexJsonRequest)
        Dim typCellRange As MyExcel.CellRangeInfo
        
        If JsonRequest.RangeType = "rows" Then
            ' 複数行の処理
            typCellRange = MyExcel.GetTargetRangeInfo(wsTargetSheet, JsonRequest.Cell, False)
            dicJson(JsonRequest.Key) = GetJsonRequestTargetByRows(wsTargetSheet, typConfig, JsonRequest, typCellRange)
        ElseIf JsonRequest.RangeType = "columns" Then
            ' 複数列の処理
            typCellRange = MyExcel.GetTargetRangeInfo(wsTargetSheet, JsonRequest.Cell, True)
            dicJson(JsonRequest.Key) = GetJsonRequestTargetByCols(wsTargetSheet, typConfig, JsonRequest, typCellRange)
        ElseIf JsonRequest.RangeType = "row" Then
            ' 行の処理
            typCellRange = MyExcel.GetTargetRangeInfo(wsTargetSheet, JsonRequest.Cell, False)
            JsonRequest.Type = "object"
            Set dicJson(JsonRequest.Key) = GetJsonRequestTargetByRow(wsTargetSheet, typConfig, JsonRequest, typCellRange, 0)
        ElseIf JsonRequest.RangeType = "column" Then
            ' 列の処理
            typCellRange = MyExcel.GetTargetRangeInfo(wsTargetSheet, JsonRequest.Cell, True)
            JsonRequest.Type = "object"
            Set dicJson(JsonRequest.Key) = GetJsonRequestTargetByCol(wsTargetSheet, typConfig, JsonRequest, typCellRange, 0)
        ElseIf JsonRequest.RangeType = "sheet" Then
            ' シート名の処理
            dicJson(JsonRequest.Key) = wsTargetSheet.Name
        Else
            ' セルの処理
            dicJson(JsonRequest.Key) = wsTargetSheet.Range(JsonRequest.Cell).Value
        End If
    Next indexJsonRequest
    
    Debug.Print JsonConverter.ConvertToJson(dicJson)
    
    Set CreateJson = dicJson
End Function

' 読込対象取得（行単位・複数）
Function GetJsonRequestTargetByRows(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As MyExcel.CellRangeInfo) As Dictionary()
    Dim lstJsonRequestTargets() As Dictionary

    ' 指定数分ループを行う
    Dim intCount As Integer
    If IsEmpty(JsonRequest.Count) Or JsonRequest.Count <= 0 Then
        intCount = typCellRange.RowCount
    Else
        intCount = JsonRequest.Count
    End If
    
    Dim indexRow As Integer: indexRow = 0
    ReDim lstJsonRequestTargets(intCount - 1)
    For indexRow = 0 To intCount - 1
        Set lstJsonRequestTargets(indexRow) = GetJsonRequestTargetByRow(wsTargetSheet, typConfig, JsonRequest, typCellRange, indexRow)
    Next indexRow
    
    GetJsonRequestTargetByRows = lstJsonRequestTargets
End Function

' 読込対象取得（行単位）
Function GetJsonRequestTargetByRow(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As CellRangeInfo, indexRow As Integer) As Dictionary
    Dim dicJsonRequestTarget As Dictionary: Set dicJsonRequestTarget = CreateObject("Scripting.Dictionary")
    If JsonRequest.Type = "object" Then
        Set dicJsonRequestTarget = GetJsonRequestTargetByRowsObjectItem(wsTargetSheet, typConfig, JsonRequest, typCellRange, indexRow)
    Else
        Set dicJsonRequestTarget = GetJsonRequestTargetByRowsCellItem(wsTargetSheet, typConfig, JsonRequest, typCellRange, indexRow)
    End If
    Set GetJsonRequestTargetByRow = dicJsonRequestTarget
End Function

' 読込対象取得（行単位_Object取得時）
Function GetJsonRequestTargetByRowsObjectItem(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As CellRangeInfo, indexRow As Integer) As Dictionary
    Dim dicJsonRequestTarget As Dictionary: Set dicJsonRequestTarget = CreateObject("Scripting.Dictionary")
    Dim indexJsonRequestTarget As Integer: indexJsonRequestTarget = 0
    For indexJsonRequestTarget = 0 To UBound(JsonRequest.Targets)
        Dim JsonRequestTarget As JsonRequestTargetInfo: JsonRequestTarget = JsonRequest.Targets(indexJsonRequestTarget)
        
        Dim strTargetCell As String: strTargetCell = JsonRequestTarget.Target + Trim(typCellRange.StartRow + indexRow)
        dicJsonRequestTarget(JsonRequestTarget.Key) = wsTargetSheet.Range(strTargetCell).Value
    Next indexJsonRequestTarget
    
    Set GetJsonRequestTargetByRowsObjectItem = dicJsonRequestTarget
End Function

' 読込対象取得（行単位_通常取得時）
Function GetJsonRequestTargetByRowsCellItem(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As CellRangeInfo, indexRow As Integer) As Object
    Dim strTargetCell As String: strTargetCell = typCellRange.StartCol + Trim(typCellRange.StartRow + indexRow)
    Set GetJsonRequestTargetByRowsCellItem = wsTargetSheet.Range(strTargetCell).Value
End Function

' 読込対象取得（列単位・複数）
Function GetJsonRequestTargetByCols(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As MyExcel.CellRangeInfo) As Dictionary()
    Dim lstJsonRequestTargets() As Dictionary

    ' 指定数分ループを行う
    Dim intCount As Integer
    If IsEmpty(JsonRequest.Count) Or JsonRequest.Count <= 0 Then
        intCount = typCellRange.ColCount
    Else
        intCount = JsonRequest.Count
    End If
    
    Dim indexCol As Integer: indexCol = 0
    ReDim lstJsonRequestTargets(intCount - 1)
    For indexCol = 0 To intCount - 1
        Set lstJsonRequestTargets(indexCol) = GetJsonRequestTargetByCol(wsTargetSheet, typConfig, JsonRequest, typCellRange, indexCol)
    Next indexCol
    
    GetJsonRequestTargetByCols = lstJsonRequestTargets
End Function

' 読込対象取得（列単位）
Function GetJsonRequestTargetByCol(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As CellRangeInfo, indexCol As Integer) As Dictionary
    If JsonRequest.Type = "object" Then
        Set GetJsonRequestTargetByCol = GetJsonRequestTargetByColsObjectItem(wsTargetSheet, typConfig, JsonRequest, typCellRange, indexCol)
    Else
        Set GetJsonRequestTargetByCol = GetJsonRequestTargetByColsCellItem(wsTargetSheet, typConfig, JsonRequest, typCellRange, indexCol)
    End If
End Function

' 読込対象取得（列単位_Object取得時）
Function GetJsonRequestTargetByColsObjectItem(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As CellRangeInfo, indexCol As Integer) As Dictionary
    Dim dicJsonRequestTarget As Dictionary: Set dicJsonRequestTarget = CreateObject("Scripting.Dictionary")

    Dim indexJsonRequestTarget As Integer: indexJsonRequestTarget = 0
    For indexJsonRequestTarget = 0 To UBound(JsonRequest.Targets)
        Dim JsonRequestTarget As JsonRequestTargetInfo: JsonRequestTarget = JsonRequest.Targets(indexJsonRequestTarget)
        dicJsonRequestTarget(JsonRequestTarget.Key) = wsTargetSheet.Cells(CInt(JsonRequestTarget.Target), typCellRange.StartColIndex + indexCol).Value
    Next indexJsonRequestTarget
    
    Set GetJsonRequestTargetByColsObjectItem = dicJsonRequestTarget
End Function

' 読込対象取得（列単位_通常取得時）
Function GetJsonRequestTargetByColsCellItem(wsTargetSheet As Worksheet, typConfig As Config, JsonRequest As JsonRequestInfo, typCellRange As CellRangeInfo, indexCol As Integer) As Object
    Set GetJsonRequestTargetByColsCellItem = wsTargetSheet.Cells(CInt(JsonRequestTarget.Target), typCellRange.StartColIndex + indexCol).Value
End Function

' 読込対象範囲情報取得
Function GetTargetRangeInfo(wsTargetSheet As Worksheet, strStartCell As String, isTargetColumn As Boolean) As CellRangeInfo
    ' 開始セル情報
    Dim celStartCell As Range
    Set celStartCell = wsTargetSheet.Range(strStartCell)
    
    ' 範囲を取得
    Dim rngAllCells As Range: Set rngAllCells = GetLastCell(wsTargetSheet, strStartCell, isTargetColumn)
    
    ' 終了セル情報
    Dim celEndCell As Range
    Set celEndCell = rngAllCells.Cells(rngAllCells.Rows.Count, rngAllCells.Columns.Count)
    
    Dim result As CellRangeInfo
    result.StartCell = Replace(celStartCell.Address, "$", "")
    result.StartCol = Split(celStartCell.Address, "$")(1)
    result.StartColIndex = celStartCell.Column
    result.StartRow = celStartCell.Row
    result.EndCell = Replace(celEndCell.Address, "$", "")
    result.EndCol = Split(celEndCell.Address, "$")(1)
    result.EndColIndex = celEndCell.Column
    result.EndRow = celEndCell.Row
    result.RowCount = celEndCell.Row - celStartCell.Row + 1
    result.ColCount = celEndCell.Column - celStartCell.Column + 1
    
    GetTargetRangeInfo = result
End Function

' 終了セル取得
Function GetLastCell(wsTargetSheet As Worksheet, strStartCell As String, isTargetColumn As Boolean) As Range
    Dim celStartCell As Range
    Set celStartCell = wsTargetSheet.Range(strStartCell)
    
    ' 範囲を取得
    Dim rngAllCells As Range
    If isTargetColumn Then
        Set rngAllCells = wsTargetSheet.Range(strStartCell, wsTargetSheet.Cells(celStartCell.Row, wsTargetSheet.Columns.Count).End(xlUp))
    Else
        Set rngAllCells = wsTargetSheet.Range(strStartCell, wsTargetSheet.Cells(wsTargetSheet.Rows.Count, celStartCell.Column).End(xlUp))
    End If
    
    ' 空セルが出現するまでの範囲を返却
    Dim strLastCell As String
    Dim oneCell As Range
    For Each oneCell In rngAllCells
        If MyCommon.IsNullOrEmpty(oneCell.Value) Then
            GoTo ExitFor
        End If
        strLastCell = Replace(oneCell.Address, "$", "")
    Next oneCell
ExitFor:
    Set GetLastCell = wsTargetSheet.Range(strStartCell, strLastCell)
End Function

' 出力ファイル名取得
Function GetExportFileName(wsTargetSheet As Worksheet, typConfig As Config, strExtention As String) As String
    Dim strFileName As String

    If typConfig.ExportFileRequest.Type = "filename" Then
        strFileName = typConfig.ExportFileRequest.FileName
    ElseIf typConfig.ExportFileRequest.Type = "sheet" Then
        strFileName = wsTargetSheet.Name
    ElseIf Not MyCommon.IsNullOrEmpty(typConfig.ExportFileRequest.Cell) Then
        strFileName = wsTargetSheet.Range(typConfig.ExportFileRequest.Cell).Value
    End If
    
    If MyCommon.IsNullOrEmpty(strFileName) Then
        strFileName = wsTargetSheet.Name
    End If
    
    strFileName = strFileName + "." + strExtention
    GetExportFileName = strFileName
End Function
